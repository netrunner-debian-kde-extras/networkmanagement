#!/usr/bin/perl -w

=head1 NAME

dh_auto_install - automatically runs make install or similar

=cut

use strict;
use Debian::Debhelper::Dh_Lib;
use Debian::Debhelper::Dh_Plugins;
use Cwd;

=head1 SYNOPSIS

B<dh_auto_install> [S<I<debhelper options>>] [S<B<--> I<params>>]

=head1 DESCRIPTION

dh_auto_install is a debhelper program that tries to automatically install
built files. If there's a Makefile and it contains a "install" target,
then this is done by running make (or MAKE, if the environment variable is
set). If there is a setup.py or Build.PL, it is used.

The files are installed into debian/<package>/ if there is only one binary
package. In the multiple binary package case, the files are instead
installed into debian/tmp/, and should be moved from there to the
appropriate package build directory using L<dh_install(1)> or
L<dh_movefiles(1)>.

DESTDIR is used to tell make where to install the files. 
If the Makefile was generated by MakeMaker from a Makefile.PL, it will
automatically set PREFIX=/usr too, since such Makefiles need that.

This is intended to work for about 90% of packages. If it doesn't work, or
tries to use the wrong install target, you're encouraged to skip using
dh_auto_install at all, and just run make install manually.

=head1 OPTIONS

=over 4

=item B<--> I<params>

Pass "params" to the program that is run. These can be used to supplement
or override the any standard parameters that dh_auto_install passes.

=back

=cut

init();

my $destdir;
my @allpackages=getpackages();
if (@allpackages > 1) {
	$destdir="debian/tmp";
}
else {
	$destdir=tmpdir($dh{MAINPACKAGE});
}
$destdir=cwd()."/".$destdir;

my $plugin = Debian::Debhelper::Dh_Plugins::find_buildsystem_plugin("install");
if ($plugin) {
	$plugin->install($destdir);
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
